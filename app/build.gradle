plugins {
    id 'java'
    id 'application'
    id 'com.google.cloud.tools.jib' version '3.4.1'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

ext {
    chatGPTVersion = '0.18.0'
}

var outputImage = 'togetherjava.org:5001/togetherjava/tjplays:master' ?: 'latest'

jib {
    from.image = 'eclipse-temurin:21'
    to {
        image = outputImage
        auth {
            username = System.getenv('ORG_REGISTRY_USER') ?: ''
            password = System.getenv('ORG_REGISTRY_PASSWORD') ?: ''
        }
    }
    container {
        mainClass = 'com.togetherjava.tjplays.App'
        setCreationTime(java.time.Instant.now().toString())
    }
}

shadowJar {
    archiveBaseName.set('TJ-Plays')
    archiveClassifier.set('')
    archiveVersion.set('')
}

dependencies {
    implementation 'net.dv8tion:JDA:5.2.1'

    implementation "com.theokanning.openai-gpt3-java:api:$chatGPTVersion"
    implementation "com.theokanning.openai-gpt3-java:service:$chatGPTVersion"

    implementation 'org.apache.logging.log4j:log4j-core:2.23.0'
    runtimeOnly 'org.apache.logging.log4j:log4j-slf4j18-impl:2.18.0'

    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
}

application {
    mainClass = 'com.togetherjava.tjplays.Bot'
}

tasks.named('test') {
    useJUnitPlatform()
}

compileJava {
    options.encoding = 'UTF-8'
}

compileTestJava {
    options.encoding = 'UTF-8'
}
